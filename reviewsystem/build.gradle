apply plugin: "java"
apply plugin: "maven"

group = "myorg"
version = 1.0

repositories {
    mavenCentral()
}

sourceSets.all { set ->
    def jarTask = task("${set.name}Jar", type: Jar) {
        baseName = baseName + "-$set.name"
        from set.output
    }

    artifacts {
        archives jarTask
    }
}

sourceSets {
    api
    impl
}

dependencies {

	compile(  
            'io.dropwizard:dropwizard-core:0.9.2',
            'io.dropwizard:dropwizard-validation:0.9.2',     
            'org.springframework:spring-core:4.2.2.RELEASE',
            'org.springframework:spring-beans:4.2.2.RELEASE',
            'org.springframework:spring-context:4.2.2.RELEASE',
            'org.hibernate:hibernate-core:5.1.0.Final',
            'org.hibernate:hibernate-entitymanager:5.1.0.Final',
            'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final',
            'org.springframework.data:spring-data-jpa:1.9.4.RELEASE',
            'commons-lang:commons-lang:2.6',
            'commons-codec:commons-codec:1.10',
            'com.jolbox:bonecp:0.8.0.RELEASE',
            'mysql:mysql-connector-java:5.1.38'
    )  

    testCompile 'junit:junit:4.9'
    testCompile sourceSets.api.output
    testCompile sourceSets.impl.output
    runtime configurations.apiRuntime
    runtime configurations.implRuntime
}

jar {
    from sourceSets.api.output
    from sourceSets.impl.output
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri("${buildDir}/repo"))

            addFilter("main") { artifact, file -> artifact.name == project.name }
            ["api", "impl"].each { type ->
                addFilter(type) { artifact, file -> artifact.name.endsWith("-$type") }
                
                // We now have to map our configurations to the correct maven scope for each pom
                ["compile", "runtime"].each { scope ->
                    configuration = configurations[type + scope.capitalize()]
                    ["main", type].each { pomName ->
                        pom(pomName).scopeMappings.addMapping 1, configuration, scope
                    }
                }
            }

        }
    }
}
